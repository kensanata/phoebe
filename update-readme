#!/usr/bin/env perl
use Modern::Perl;
use Pod::Markdown;
use Text::Slugify qw(slugify);
use File::Slurper qw(read_text write_text);
die "Read which Perl script?" unless @ARGV;
my @pods = map { read_text($_) } @ARGV;
for (@pods) {
  s/^=head(\d) NAME\n\n(\S+).*/=head$1 $2/m; # replace NAME with the lib on the next paragraph
  s/^=head(\d) DESCRIPTION\n\n//m;
  s/^=head(\d) SYNOPSIS\n\n//m;
  s/^=head(\d) ([A-Z ]+)$/"=head$1 ".ucfirst(lc($2))/gem; # change OPTIONS to Options
}
my $pod = join("\n", @pods);
my $parser = Pod::Markdown->new;
my $markdown;
$parser->output_string(\$markdown);
$parser->parse_string_document($pod);
my @toc = $pod =~ /\n=head[12] (.*)/g;
foreach (@toc) {$_ = ucfirst lc if /^[A-Z ]+$/ }
my $toc = "**Table of Contents**\n\n"
    . join("\n", map { my $item = $_; s/::/-/g; "- [$item](#" . slugify($_) . ")" } @toc)
    . "\n\n";
write_text('README.md', "# Phoebe Wiki\n\n" . $toc . $markdown);
